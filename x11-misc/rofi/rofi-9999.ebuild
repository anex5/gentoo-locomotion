# Copyright 1999-2023 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

inherit meson toolchain-funcs xdg-utils

MY_PV="${PV}+wayland2"

DESCRIPTION="A window switcher, run dialog and dmenu replacement (fork with Wayland support)"
HOMEPAGE="https://github.com/lbonn/rofi"

if [[ ${PV} = *9999 ]]; then
	inherit git-r3
	EGIT_REPO_URI="https://github.com/lbonn/rofi"
	#EGIT_SUBMODULES=()
	EGIT_BRANCH="wayland"
	KEYWORDS=""
else
	SRC_URI="https://github.com/lbonn/rofi/releases/download/${MY_PV}/${PN}-${MY_PV}.tar.xz"
	KEYWORDS="~amd64 ~x86 ~arm ~arm64"
	S=${WORKDIR}/${PN}-${MY_PV}
fi

LICENSE="MIT"
SLOT="0"
IUSE="X wayland +drun +imdkit test +windowmode man"
RESTRICT="!test? ( test )"

BDEPEND="
	dev-util/meson
	virtual/pkgconfig
	wayland? ( >=dev-libs/wayland-protocols-1.17 )
"
RDEPEND="
	dev-libs/glib:2
	x11-libs/gdk-pixbuf:2
	x11-libs/cairo[X?]
	X? (
		x11-libs/startup-notification
		x11-libs/xcb-util
		x11-libs/xcb-util-cursor
		x11-libs/xcb-util-wm
		x11-misc/xkeyboard-config
	)
	x11-libs/libxkbcommon[X?]
	x11-libs/pango[X?]
	wayland? (
		>=dev-libs/wayland-protocols-1.17
		>=dev-libs/wayland-1.17.0
	)
"
DEPEND="
	${RDEPEND}
	X? ( x11-base/xorg-proto )
	test? ( >=dev-libs/check-0.11 )
"
RESTRICT="mirror"


src_prepare() {
	use man || sed -i -e "/subdir('doc')/d" -e '/install_man(/{:1;/)/!{N;b1};d}' meson.build || die
	use test || sed -i -e '/test(/{:1;/))/!{N;b1};d}' meson.build || die
	default
}

src_configure() {
	tc-export CC CXX LD
	local emesonargs=(
		$(meson_use drun)
		$(meson_use windowmode window)
		$(meson_feature X xcb)
		$(meson_feature wayland)
		$(meson_feature test check)
		$(meson_use imdkit imdkit)
	)

	meson_src_configure
}

src_install() {
	meson_src_install
	use man && doman doc/*
}

pkg_postinst() {
	for v in ${REPLACING_VERSIONS}; do
		if ver_test "${v}" -lt 1.7.0; then
			elog "Rofi 1.7.0 removed the (deprecated) xresources based configuration setup."
			elog "If you are still using old configuration setup, please convert it to new format manually."
			elog "The new format configuration can be generated by 'rofi -dump-config > ~/.config/rofi/config.rasi'."
			elog "For more information, please see https://github.com/davatorium/rofi/releases/tag/1.7.0"
		fi
	done

	xdg_icon_cache_update
}

pkg_postrm() {
	xdg_icon_cache_update
}
