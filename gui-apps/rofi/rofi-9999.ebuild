# Copyright 1999-2025 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

inherit meson toolchain-funcs xdg-utils

DESCRIPTION="A window switcher, run dialog and dmenu replacement (fork with Wayland support)"
HOMEPAGE="https://github.com/davatorium/rofi"

if [[ ${PV} = *9999 ]]; then
	inherit git-r3
	EGIT_REPO_URI="https://github.com/davatorium/rofi"
	#EGIT_SUBMODULES=()
	EGIT_BRANCH="next"
	KEYWORDS=""
else
	MY_PV="${PV/_/-}"
	LIBGWATER_COMMIT="d86f9903efb9c490c0e3b0316d7f2da5b5a5632c"
	LIBNKUTILS_COMMIT="2f220a40ad32cf51b6b7d7ae83ab641a3ae76693"
	SRC_URI="
		https://github.com/davatorium/rofi/archive/refs/tags/${MY_PV}.tar.gz -> ${P}.tar.gz
		https://github.com/sardemff7/libgwater/archive/${LIBGWATER_COMMIT}.tar.gz
		https://github.com/sardemff7/libnkutils/archive/${LIBNKUTILS_COMMIT}.tar.gz
	"
	KEYWORDS="~amd64 ~x86 ~arm ~arm64"
	S=${WORKDIR}/${PN}-${MY_PV}
	RESTRICT="mirror"
fi

LICENSE="MIT"
SLOT="0"
IUSE="X doc +drun examples imdkit man test wayland +windowmode"
REQUIRED_USE="
	|| ( X wayland )
	imdkit? ( X )
"
RESTRICT+="!test? ( test )"

BDEPEND="
	dev-build/meson
	app-alternatives/ninja
	virtual/pkgconfig
	virtual/libc
	doc? ( app-text/doxygen[dot] )
	man? ( virtual/pandoc )
	wayland? ( >=dev-libs/wayland-protocols-1.17 )
"
RDEPEND="
	dev-libs/glib:2
	x11-libs/gdk-pixbuf:2
	x11-libs/cairo[X?]
	x11-libs/libxkbcommon[X?]
	x11-libs/pango[X?]
	X? (
		x11-libs/startup-notification
		x11-libs/xcb-util
		x11-libs/xcb-util-cursor
		x11-libs/xcb-util-wm
		x11-misc/xkeyboard-config
	)
	imdkit? ( x11-libs/xcb-imdkit )
	wayland? (
		>=dev-libs/wayland-protocols-1.17
		>=dev-libs/wayland-1.17.0
	)
	!x11-misc/rofi
"
DEPEND="
	${RDEPEND}
	X? ( x11-base/xorg-proto )
	test? (
		>=dev-libs/check-0.11
		dev-util/cppcheck
	)
"
RESTRICT="mirror"

PATCHES=(
	"${FILESDIR}"/rofi-9999-filebrowser-mesg-path.patch
	"${FILESDIR}"/rofi-9999-filebrowser-regex.patch
	"${FILESDIR}"/rofi-9999-fix-theme-selector-home.patch
	"${FILESDIR}"/rofi-2.0.0-allow-icons-tint.patch
	"${FILESDIR}"/rofi-2.0.0-fix-async-mode-multiselect.patch
	"${FILESDIR}"/rofi-2.0.0-fix-missing-and-wrong-free.patch
)

src_prepare() {
	if use !man && use !doc; then
		sed -e "/subdir('doc')/d" -i meson.build || die
	fi
	use !man && ( sed -e '/install_man(/{:1;/)/!{N;b1};d}' -i doc/meson.build || die )
	use !doc && ( sed -e '/if doxygen.found(/{:1;/endif/!{N;b1};d}' -i doc/meson.build || die )
	use !test && ( sed -i -e '/test(/{:1;/))/!{N;b1};d}' meson.build || die )
	#use touch && eapply "${FILESDIR}"/rofi-2.0.0-touch-support-pr143.patch
	if [[ ${PV} != *9999 ]]; then
		rm -rf subprojects/libgwater
		ln -sv "${WORKDIR}"/libgwater-${LIBGWATER_COMMIT} subprojects/libgwater
		rm -rf subprojects/libnkutils
		ln -sv "${WORKDIR}"/libnkutils-${LIBNKUTILS_COMMIT} subprojects/libnkutils
	fi
	default
}

src_configure() {
	# Doesn't work with reflex, bug #887049
	export LEX=flex

	# Requires bison, see https://bugs.gentoo.org/894634.
	unset YACC

	tc-export CC CXX LD
	local emesonargs=(
		$(meson_use drun)
		$(meson_use windowmode window)
		$(meson_feature X xcb)
		$(meson_feature wayland)
		$(meson_feature test check)
		$(meson_use imdkit imdkit)
	)

	meson_src_configure
}

src_install() {
	meson_src_install
	use examples && ( insinto /usr/share/rofi/examples/; doins Examples/*.sh || die "Examples install failed.")
	cp "${FILESDIR}/rofi.svg" "${D}/usr/share/icons/hicolor/scalable/apps/" || die "Rofi default icon install failed."
}

src_test() {
	meson_src_test
}

pkg_postinst() {
	for v in ${REPLACING_VERSIONS}; do
		if ver_test "${v}" -lt 1.7.0; then
			elog "Rofi 1.7.0 removed the (deprecated) xresources based configuration setup."
			elog "If you are still using old configuration setup, please convert it to new format manually."
			elog "The new format configuration can be generated by 'rofi -dump-config > ~/.config/rofi/config.rasi'."
			elog "For more information, please see https://github.com/davatorium/rofi/releases/tag/1.7.0"
		fi
	done

	xdg_icon_cache_update
}

pkg_postrm() {
	xdg_icon_cache_update
}
